plugins {
    id 'java'
    id 'war'
}

group 'edu.bbte.idde.leim2041'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'
    implementation project(':leim2041-backend')
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'

}
def deploymentName = 'leim2041-web'

def tomcatHome = System.getenv('CATALINA_HOME')

logger.quiet("Tomcat home detected as '${tomcatHome}'")

task deploy(type: Copy, dependsOn: 'war') {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile

    rename war.archiveFileName.get(), "${deploymentName}.war"

    into "${tomcatHome}/webapps"
}

task undeploy(type: Delete) {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }
    delete "${tomcatHome}/webapps/${deploymentName}.war"
}


test {
    useJUnitPlatform()
}
